
    final double LOW = 0.000001;
    final double HIGH = 0.999999;
    
    @In(1) PImage in;
    
    @P(1) @Type.Number(min=0, max=60, def=0)
    double fadeTime;
    @P(2) @Type.Number(min=0, max=1, def=1)
    double mix;
    @P(100) @ReadOnly
    Property activeMix;
    
    @T(1) Trigger trigger;
    @T(2) Trigger reset;
    
    @OffScreen PGraphics fg, bg, scratch;

    
    @Override
    public void setup() {

    }

    @Override
    public void draw() {
        if (reset.poll()) {
            reset();
        }
        
        if (trigger.poll()) {
            capture();
        } else {
            render();
        }
        
    }

    void capture() {
        double am = d(activeMix);
        if (am > LOW) {
            if (am > HIGH) {
                swapFgBg();
            } else {
                mix(fg, bg, am);
            }
            activeMix.set(0);
        }
        fg.clear();
        if (fadeTime < LOW) {
            if (mix > HIGH) {
                bg.copy(in);
                copy(in);
                release(in);
            } else {
                mix(in, bg, mix);
                copy(bg);       
            }
        } else {
            fg.copy(in);
            copy(bg);
            activeMix.set(0).to(mix).in(fadeTime);
        }
        
    }
    
    void render() {
        double am = d(activeMix);
        if (am > LOW) {
            if (activeMix.isAnimating()) {
                blendMode(ADD, 1 - am);
                image(bg, 0, 0);
                blendMode(ADD, am);
                image(fg, 0, 0);
            } else {
                if (am > HIGH) {
                    swapFgBg();
                } else {
                    mix(fg, bg, am);
                }
                copy(bg);
                activeMix.set(0);
            }
        } else {
            copy(bg);
        }
    }
    
    void mix(PImage src, PGraphics dstIn, double amount) {
        scratch.beginDraw();
        scratch.blendMode(ADD, 1 - amount);
        scratch.image(dstIn, 0, 0);
        scratch.blendMode(ADD, amount);
        scratch.image(src, 0, 0);
        scratch.endDraw();
        dstIn.copy(scratch);
        scratch.clear();
    }
    
    void reset() {
        fg.clear();
        bg.clear();
        activeMix.set(0);
    }
    
    void swapFgBg() {
        PGraphics tmp = fg;
        fg = bg;
        bg = tmp;
    }
